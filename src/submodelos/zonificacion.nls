globals [ 
  ;; TODO: creo que el calculo en vivo de estas celdas si alenta el modelo :( Recuerda que se pueden calcular después de agregar todos los elementos de zonificación
  celdas_restriccion
  celdas_libre
  celdas_protegido
  celdas_NA
]

to init_zonificacion
  print "Inicializando zonificación..."
  ask celdas_tierra [ set zonificacion "NA" ]
  ask celdas_mar    [ set zonificacion "libre" ]
  
  ;; se inicializan las áreas protegidas
  let x_origen_zona_protegida (min [pxcor] of celdas_tierra) - 1
  let y_origen_zona_protegida (max [pycor] of patches)
  
  ask patches with [ 
    pxcor >  x_origen_zona_protegida - LARGO_ZONA_PROTEGIDA and 
    pxcor <= x_origen_zona_protegida and
    pycor >  y_origen_zona_protegida - ANCHO_ZONA_PROTEGIDA and
    pycor <= y_origen_zona_protegida 
  ][ set zonificacion "protegido" ]
  
  actualizar_zonificacion
  
  print "OK"
end

to actualizar_zonificacion
  set celdas_restriccion patches with [ zonificacion = "restriccion" ]
  set celdas_libre patches with [ zonificacion = "libre" ]
  set celdas_protegido patches with [ zonificacion = "protegido" ]
  set celdas_NA patches with [ zonificacion = "NA" ]
end

to SELECCIONAR_ZONAS_PROTEGER
  ;; TODO: desactivar los otros botones activos
  ifelse mouse-down? [
    ask patch mouse-xcor mouse-ycor [
      if not ya_me_picaste [
        set ya_me_picaste true 
        ifelse zonificacion = "libre" or zonificacion = "restriccion" 
        [ set zonificacion "protegido" ]
        [ set zonificacion "libre" ]
      ]
    ]
    
    actualizar_zonificacion
    actualizar_transitables_embarcaciones
    ask embarcaciones [ actualizar_sitios_pesca_mejor_sitio_y_sitios_visitados ]
    
    colorear_celdas
;    clear-drawing
;    dibujar_regiones
;    dibujar_borde celdas_restriccion
;    dibujar_borde celdas_protegido
    display
  ][
    ask patches [ set ya_me_picaste false ]
  ]
  ;; TODO: tratar casos extremos como que declaren toda la zona pesquera como protegida
end

to SELECCIONAR_ZONAS_RESTRICCION
  ;; TODO: desactivar los otros botones activos
  ifelse mouse-down? [
    ask patch mouse-xcor mouse-ycor [
      if not ya_me_picaste [
        set ya_me_picaste true 
        ifelse zonificacion = "libre" or zonificacion = "protegido" 
        [ set zonificacion "restriccion" ]
        [ set zonificacion "libre" ]
      ]  
    ]
    actualizar_zonificacion
    actualizar_transitables_embarcaciones
    ask embarcaciones [ actualizar_sitios_pesca_mejor_sitio_y_sitios_visitados ]
    
    colorear_celdas
;    clear-drawing
;    dibujar_regiones
;    dibujar_borde celdas_restriccion
;    dibujar_borde celdas_protegido
    display
    if not todos_los_sitios_son_accesibles? [
      user-message "Advertencia: No todas celdas para pescar son accesibles a las embarcaciones, pude que el modelo falle.\n Intenta hacer un paisaje en el que se pueda acceder a cualquier sitio de pesca." 
    ]
    init_rutas
  ][
    ask patches [ set ya_me_picaste false ]
  ]
  ;; TODO: tratar casos extremos como que declaren toda la zona pesquera como protegida o que dejen
  ;; a una embarcacion sin posibilidad de regresar a su puerto
end

to COLOCAR_PLATAFORMAS
  ifelse mouse-down? [
    ask patch mouse-xcor mouse-ycor [
      if not ya_me_picaste [
        set ya_me_picaste true
        ;; supuesto: solo se puede poner una plataforma por pixel
        ifelse tipo = "mar" [
          ifelse zonificacion != "protegido" [
            ifelse not any? plataformas-here [
              instalar_plataforma
            ][
              user-message "Aquí ya hay una plataforma. Busca otro lugar para colocar una nueva."
            ]
          ][
            user-message "Esta es un area protegida, aquí no se pueden colocar plataformas."
          ]
        ][
          user-message "No se pueden colocar plataformas en la tierra."
        ]
      ]
    ]
    actualizar_zonificacion
    actualizar_transitables_embarcaciones
    ask embarcaciones [ actualizar_sitios_pesca_mejor_sitio_y_sitios_visitados ]
    colorear_celdas
    display
    if not todos_los_sitios_son_accesibles? [
      user-message "Advertencia: No todas celdas para pescar son accesibles a las embarcaciones, pude que el modelo falle.\n Intenta hacer un paisaje en el que se pueda acceder a cualquier sitio de pesca." 
    ]
    init_rutas
  ]
  [
    ask patches [ set ya_me_picaste false ]
  ]
end