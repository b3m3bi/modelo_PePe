breed [ embarcaciones embarcacion ]
directed-link-breed [ amistades amistad ]

embarcaciones-own [
  mi_puerto                        ;; agent (puerto): el agente puerto que representa el puerto donde embarco y descargo
  region_pesca                     ;; number: número de la región en la que la embarcación pesca
  tiempo_restante_iteracion        ;; number: contador del tiempo de la iteración que le queda a la embarcacion para hacer actividades
  estado                           ;; string: descripción del estado de actividad de la embarcación ("descansando", "planeando", "moviendose", "pescando", "desembarcando")
  tiempo_descansado                ;; number: contador del tiempo que la embaración ha descansado
  tipo_planeacion                  ;; string: el tipo de planeación de viaje que debe hacer la embarcación ("inicio", "continuar", "regreso")  
  destino                          ;; patch: celda a la que me dirijo
  posibles_sitios_pesca            ;; agentset (patches): conjunto de celdas donde una embarcacion puede pescar
  mejor_sitio                      ;; patch: celda que me ha dado una alta ganancia en ocaciones previas. Usado por algoritmo EEI.
  sitios_visitados                 ;; list (patches): celdas en las que una embarcación pescó durante un viaje
  ruta                             ;; list (patches): lista ordenada de las celdas que debo seguir para llegar a mi destino
  indice_actual_ruta               ;; number: índice dentro de la lista de ruta de la celda donde estoy actualmente
  capturas_viaje                   ;; list (list (number)): registra las capturas de cada especie en cada sitio visitado
  viajes_finalizados               ;; number: registra el número de viajes completados que ha realizado una embarcación
  distancia_recorrida              ;; number: distancia recorrida por una embarcación en un viaje
  distancia_recorrida_ultimo_viaje ;; number: registra la ditancia que la embarcación recorrió durante el último viaje completado
  horas_en_mar                     ;; number: número de horas que la embarcación lleva en el mar durante un viaje
  horas_en_mar_ultimo_viaje        ;; number: registra el número de horas que la embarcación estuvo en el mar durante el último viaje completado
  captura_ultimo_viaje             ;; number: registra la captura que la embarcación obtuvo durante el último viaje completado
  ganancia                         ;; number: registra la ganancia obtenida por la embarcación
  ganancia_por_hora                ;; number: ganancia por hora de viaje  
  ganancia_por_hora_ultimo_viaje   ;; number: registra la ganancia obtenida por la embarcación durante su ultimo viaje
  saldo_subsidio_gasolina          ;; number: saldo disponible para un pescador para pagar la gasolina
  gasto_gasolina                   ;; number: gasto de gasolina de un pescador por viaje
  ingreso_venta_captura            ;; number: ingreso de la venta de la captura de un pescador por viaje
  capturabilidad                   ;; number: porcentaje de la biomasa disponible en una celda que pesca una embarcación cuando despliega sus artes de pesca
  capacidad_maxima                 ;; number: capacidad de carga de biomasa máxima que tiene una embarcación
  horas_maximas_en_mar             ;; number: número máximo de horas que una embarcación pasa en el mar
  horas_descansar                  ;; number: número de horas que una embarcación descansa en el puerto
  ganancia_mensual                 ;; number: registra la ganancia mensual que genera la embarcación
  salario_mensual_tripulacion      ;; number: registra el salario mensual que recibe la tripulación 
  tamanio_tripulacion              ;; number: tamaño promedio de la tripulación de la embarcación
  estado_economico                 ;; string: describe la viabilidad económica de la embarcación ("viable", "crisis", "quiebra")
  activo?                          ;; boolean: indica si una embarcación está activa o no
  meses_crisis                     ;; number: indica cuantos meses lleva una embarcación en estado de crisis
  averidado?
  inactivo_por_veda?               ;; boolean: indica si una embarcación inactiva por respetar una veda
  especies_pescadas                ;; list (number): indica que se pesca (1) o no (0) a la especie en el índice
  tortugas_matadas                 ;; number: número de tortugas que he la embarcación ha matado
  
  movimiento_acumulado
]

globals [
  precios_biomasa
]

to init_embarcacion [_puerto _reg_pesca]
  set mi_puerto _puerto
  set region_pesca _reg_pesca
  set tiempo_restante_iteracion horas_iteracion
  set estado "descansando"
  set tipo_planeacion ""
  set destino nobody
  set posibles_sitios_pesca celdas_libre with [num_region = [region_pesca] of myself]
  set mejor_sitio one-of posibles_sitios_pesca
  set sitios_visitados []
  set ruta []
  set indice_actual_ruta 0
  set capturas_viaje []
  set viajes_finalizados 0
  set distancia_recorrida 0
  set distancia_recorrida_ultimo_viaje 0
  set horas_en_mar 0
  set horas_en_mar_ultimo_viaje 0 
  set captura_ultimo_viaje 0
  set ganancia 0
  set ganancia_por_hora 0
  set ganancia_por_hora_ultimo_viaje 0
  set saldo_subsidio_gasolina 0
  set gasto_gasolina 0
  set ingreso_venta_captura 0 
  (ifelse 
    [num_puerto] of mi_puerto = 0 [
      set capturabilidad CAPTURABILIDAD_PUERTO_1
      set capacidad_maxima CAPACIDAD_MAXIMA_PUERTO_1
      set especies_pescadas read-from-string ESPECIES_PESCA_PUERTO_1
      set horas_maximas_en_mar HORAS_MAXIMAS_EN_MAR_PUERTO_1
      set tamanio_tripulacion TAMANIO_TRIPULACION_PUERTO_1
      set horas_descansar HORAS_DESCANSAR_PUERTO_1
    ]
    [num_puerto] of mi_puerto = 1 [
      set capturabilidad CAPTURABILIDAD_PUERTO_2
      set capacidad_maxima CAPACIDAD_MAXIMA_PUERTO_2
      set especies_pescadas read-from-string ESPECIES_PESCA_PUERTO_2
      set horas_maximas_en_mar HORAS_MAXIMAS_EN_MAR_PUERTO_2
      set tamanio_tripulacion TAMANIO_TRIPULACION_PUERTO_2
      set horas_descansar HORAS_DESCANSAR_PUERTO_2
    ]
    [num_puerto] of mi_puerto = 2 [
      set capturabilidad CAPTURABILIDAD_PUERTO_3
      set capacidad_maxima CAPACIDAD_MAXIMA_PUERTO_3
      set especies_pescadas read-from-string ESPECIES_PESCA_PUERTO_3
      set horas_maximas_en_mar HORAS_MAXIMAS_EN_MAR_PUERTO_3
      set tamanio_tripulacion TAMANIO_TRIPULACION_PUERTO_3
      set horas_descansar HORAS_DESCANSAR_PUERTO_3
    ])
  set ganancia_mensual 0
  set salario_mensual_tripulacion 7000
  set estado_economico "viable"
  set activo? true
  set meses_crisis 0
  set averidado? false
  set inactivo_por_veda? false
  set tiempo_descansado random horas_descansar
  set tortugas_matadas 0
  
  set shape "barco1"
  set size 1.1 
  set color item ([num_puerto] of mi_puerto) colores_puertos_embarcaciones
  move-to mi_puerto 
end

to init_embarcaciones
  print "Inicializando embarcaciones..."
  let nums_embarcaciones (list NUM_EMBARCACIONES_PUERTO_1 NUM_EMBARCACIONES_PUERTO_2 NUM_EMBARCACIONES_PUERTO_3)
  let regs_pesca (list REGION_PESCA_EMBARCACIONES_PUERTO_1 REGION_PESCA_EMBARCACIONES_PUERTO_2 REGION_PESCA_EMBARCACIONES_PUERTO_3) 
  show "Entrarndo al foreach..."
  foreach (range NUM_PUERTOS) [
    i -> 
    ask puertos with [num_puerto = i] [
      hatch-embarcaciones (item i nums_embarcaciones) [
        init_embarcacion myself (item i regs_pesca)
      ]
    ]
  ]
  show "Haciendo amistades..."
  foreach sort puertos [
    p ->
    let embarcaciones_de_puerto embarcaciones with [mi_puerto = p]
    ask embarcaciones_de_puerto [
      ifelse count embarcaciones_de_puerto > NUM_AMIGOS [
        create-amistades-to (n-of NUM_AMIGOS other embarcaciones_de_puerto) [ hide-link ]
      ][
        print (word "Advertencia: No hay suficientes embarcaciones en el puerto " [num_puerto] of mi_puerto " para formar NUM_AMIGOS = " NUM_AMIGOS " amistades. No se creará ninguna amistad para las embarcaciones de este puerto." )
      ]
    ]
  ]
  set precios_biomasa read-from-string PRECIOS_KILO_BIOMASA
  
  let regs_pesca_unicas remove-duplicates (sublist regs_pesca 0 NUM_PUERTOS)
  foreach regs_pesca_unicas [
    r -> 
    ask patches with [ num_region = r ][
      set vecindad_moore_radio (vecindad_moore RADIO_EXPLORAR false)
    ]
  ]
  
  print "OK"
end

to reset_registros_mensuales_embarcacion
  set ganancia_mensual 0
  set salario_mensual_tripulacion 0
end
  
to dinamica_iteracion_embarcacion
  set tiempo_restante_iteracion horas_iteracion
  ejecutar_estado    
end


to ejecutar_estado
  if tiempo_restante_iteracion > 0 and activo? [
    (ifelse 
      estado = "descansando"   [ descansar ]
      estado = "planeando"     [ planear ]
      estado = "moviendose"    [ moverse ]
      estado = "pescando"      [ pescar ]
      estado = "desembarcando" [ desembarcar ]
    )
    ejecutar_estado
  ]
end

to descansar
  ifelse hay_veda [ set inactivo_por_veda? true ][ set inactivo_por_veda? false ]
  ifelse not inactivo_por_veda? [
    let tiempo_descanso_ahora 0
    ifelse horas_descansar - tiempo_descansado > tiempo_restante_iteracion  [
      ;; si el tiempo restante es menor al necesario para descansar, descanso todo el tiempo y me quedo descansando (no cambio estado)...
      set tiempo_descanso_ahora tiempo_restante_iteracion
    ][
      ;; si el tiempo restante es mayor o igual al necesario para descansar, solo descanso el rato suficiente y me pongo a planear (cambio de estado)...
      set tiempo_descanso_ahora horas_descansar - tiempo_descansado 
      set estado "planeando"
      set tipo_planeacion "inicio"
    ]
    
    ;; actualizo mis tiempos
    set tiempo_descansado tiempo_descansado + tiempo_descanso_ahora
    set tiempo_restante_iteracion tiempo_restante_iteracion - tiempo_descanso_ahora
  ][
    ;; si estoy en veda solo hago que pase el tiempo
    set tiempo_restante_iteracion tiempo_restante_iteracion - tiempo_restante_iteracion
  ]
  
end

to planear
  (ifelse 
    tipo_planeacion = "inicio" [
      set destino seleccionar_sitio_pesca
      set ruta item [num_puerto] of mi_puerto [rutas_hacia_mi] of destino
    ]
    tipo_planeacion = "continuar" and CAMBIAR_SITIO_PESCA? [
      (ifelse 
        SELELECCION_SITIO_PESCA_CONTINUACION = "EEI" [
          set destino seleccionar_sitio_pesca
        ]
        SELELECCION_SITIO_PESCA_CONTINUACION = "mejor vecino" [
          set destino seleccionar_mejor_sitio_vecino
        ]
        SELELECCION_SITIO_PESCA_CONTINUACION = "un vecino" [
          set destino one-of [([posibles_sitios_pesca] of myself) at-points vecindad_moore_radio] of patch-here 
          if destino = nobody [ show "Estoy perdido" ]
      ])
      set ruta (obtener_ruta_A_star_embarcacion patch-here destino)
    ]
    tipo_planeacion = "regreso" [ 
      set destino [patch-here] of mi_puerto
      set ruta item [num_puerto] of mi_puerto [rutas_hacia_puertos] of patch-here
  ])
  
  ;; se calcula la ruta al destino elegido
;  set ruta (obtener_ruta_A_star patch-here destino)
  
  set indice_actual_ruta 0
  
  set estado "moviendose"
end

to-report seleccionar_sitio_pesca
  (ifelse
    SELECCION_SITIO_PESCA = "EEI en radio" or SELECCION_SITIO_PESCA = "EEI en region" or SELECCION_SITIO_PESCA = "EEI en radio y region" [ report seleccionar_sitio_EEI ]
    SELECCION_SITIO_PESCA = "aleatorio" [ report seleccionar_sitio_aleatorio ])
end

to-report seleccionar_sitio_EEI
  ;; se actoan los sitios pesca a solo los vecinos en un radio del mejor sitio
  let _posibles_sitios_pesca nobody
  if SELECCION_SITIO_PESCA = "EEI en radio" [
    set _posibles_sitios_pesca [([posibles_sitios_pesca] of myself) at-points vecindad_moore_radio] of mejor_sitio
  ]
  if SELECCION_SITIO_PESCA = "EEI en region" [
    set _posibles_sitios_pesca posibles_sitios_pesca
  ]
  if SELECCION_SITIO_PESCA = "EEI en radio y region" [
    ifelse random-float 1.0 < PROB_EXP_REGION [
      set _posibles_sitios_pesca posibles_sitios_pesca
    ][
      set _posibles_sitios_pesca [([posibles_sitios_pesca] of myself) at-points vecindad_moore_radio] of mejor_sitio
    ]
  ]
  
  ifelse random-float 1.0 < EPSILON [
    ;; explorar
    report one-of _posibles_sitios_pesca
  ][
    ifelse any? out-amistad-neighbors [
      ;; imitar
      let amigo_mas_exitoso max-one-of out-amistad-neighbors [ganancia_por_hora]
      ifelse [ganancia_por_hora] of amigo_mas_exitoso > ganancia_por_hora [
        report [mejor_sitio] of amigo_mas_exitoso
      ][
        ;; explotar
        report mejor_sitio
      ]
    ][
      ;; explotar
      report mejor_sitio
    ]
  ]  
end

to-report seleccionar_mejor_sitio_vecino
  report max-one-of (posibles_sitios_pesca at-points vecindad_moore_radio) [sum biomasas]
end

to-report seleccionar_sitio_aleatorio
  report one-of posibles_sitios_pesca
end


to moverse
  ifelse patch-here = destino [
    ;; ya llegué a mi destino
    ifelse destino = [patch-here] of mi_puerto 
    [ set estado "desembarcando" ]
    [ set estado "pescando" ]
  ][
    ;; avanzo una unidad de velocidad hacia mi destino
    let tamanio_paso VELOCIDAD / longitud_celda
    let distancia_avanzada 0

    let llegue? false
    while [ distancia_avanzada < tamanio_paso and not llegue? ] [
      let siguiente_celda (item indice_actual_ruta ruta)
      face siguiente_celda
      let distancia_avanzo min (list (tamanio_paso - distancia_avanzada) (distance siguiente_celda))
      jump distancia_avanzo

      set distancia_avanzada distancia_avanzada + distancia_avanzo
      set distancia_recorrida distancia_recorrida + distancia_avanzo

      ;; reviso si ya llegué al centro o no de la siguiente celda
      if distance siguiente_celda = 0 [
        ;; si ya llegué cambio de indice 
        set indice_actual_ruta indice_actual_ruta + 1
        
        ;; puede que llegue al destino y me hayan sobrado pasos, por lo que me detengo
        if patch-here = destino [ set llegue? true ]
      ]
    ]
    set horas_en_mar horas_en_mar + 1
    set tiempo_restante_iteracion tiempo_restante_iteracion - 1
  ]
end

to moverse2 
   ifelse patch-here = destino [
    ;; ya llegué a mi destino
    ifelse destino = [patch-here] of mi_puerto 
    [ set estado "desembarcando" ]
    [ set estado "pescando" ]
  ][
   if indice_actual_ruta < (length ruta) - 1 [
      set movimiento_acumulado movimiento_acumulado + (VELOCIDAD / longitud_celda)
      while [movimiento_acumulado >= 1 and indice_actual_ruta < (length ruta) - 1][
        set distancia_recorrida distancia_recorrida + distance (item (indice_actual_ruta + 1) ruta)
        set  indice_actual_ruta indice_actual_ruta + 1
        set  movimiento_acumulado movimiento_acumulado - 1
      ]
    ]
    move-to (item indice_actual_ruta ruta)
    set horas_en_mar horas_en_mar + 1
    set tiempo_restante_iteracion tiempo_restante_iteracion - 1
  ]
end

to pescar
  ;; pesco...
  ;; la capturabilidad se calcula con respecto a la proporción de las especies pescables en el parche
  
  let biomasa_total_especies_pescables_en_sitio sum ( map [[b ep] -> b * ep] biomasas especies_pescadas)
  let proporcion_spp_pescables_en_sitio n-values _num_especies [0]
  if biomasa_total_especies_pescables_en_sitio > 0 [
    set proporcion_spp_pescables_en_sitio  (map [[b ep] -> (b * ep) / biomasa_total_especies_pescables_en_sitio] biomasas especies_pescadas)
  ]
  
  let capturabilidades_sitio (map [ p -> p * CAPTURABILIDAD] proporcion_spp_pescables_en_sitio)
  let capturas_sitio (map [[b c] -> b * c] biomasas capturabilidades_sitio)
  set biomasas (map [[b c] -> b - c] biomasas capturas_sitio)

  ;; mortalidad tortugas
  if any? tortugas-here and random-float 1.0 < PROB_MORTALIDAD_TORTUGA_POR_PESCA [
    ask one-of tortugas-here [ die ]
    set tortugas_matadas tortugas_matadas + 1
  ]

  set capturas_viaje lput capturas_sitio capturas_viaje

  ;; consumo tiempo...
  set horas_en_mar horas_en_mar + 1
  set tiempo_restante_iteracion tiempo_restante_iteracion - 1

  set sitios_visitados lput patch-here sitios_visitados

;  set contador_pesca contador_pesca + 1

;  ;; visualizacion
;  if VISUALIZAR_PROCESOS_PESCADOR? [
;    set pcolor magenta
;    display
;  ]

  ;; decido qué pasa después
  ifelse obtener_captura_total_viaje >= CAPACIDAD_MAXIMA  [
    ;; ya se llenó el barco...
    ;; regreso el extra considerando solo la proporción de la última captura
    let cantidad_regresar obtener_captura_total_viaje - CAPACIDAD_MAXIMA
    let ultimas_capturas last capturas_viaje
    let proporcion_regresar (cantidad_regresar / (sum ultimas_capturas))
    let biomasas_regresar (map [ b -> b * proporcion_regresar] ultimas_capturas)

    let ultimas_capturas_actualizadas (map [ [a b] ->  a - b ] ultimas_capturas biomasas_regresar)
    set capturas_viaje replace-item ((length capturas_viaje) - 1) capturas_viaje ultimas_capturas_actualizadas
    set biomasas (map [[a b] -> a + b] biomasas biomasas_regresar)

    set estado "planeando"
    set tipo_planeacion "regreso"
  ][
    ifelse horas_en_mar > horas_maximas_en_mar [
      ;; ya estuve mucho tiempo en el mar... planea el regreso
      set estado "planeando"
      set tipo_planeacion "regreso"
    ][
      ;; planea la siguiente pesca...
      set estado "planeando"
      set tipo_planeacion "continuar"
    ]
  ]
end

to-report obtener_captura_total_viaje
  report sum (map [c -> sum c] capturas_viaje)
end

to desembarcar
  
  set viajes_finalizados viajes_finalizados + 1

  set horas_en_mar_ultimo_viaje horas_en_mar
  set distancia_recorrida_ultimo_viaje distancia_recorrida

  ;; se registra la captura y se calcula la ganancia
  set captura_ultimo_viaje obtener_captura_total_viaje
  set ganancia obtener_balance_pesca
  set ganancia_por_hora (ganancia / horas_en_mar_ultimo_viaje)
  
  set ganancia_mensual ganancia_mensual + ganancia
  set salario_mensual_tripulacion ganancia_mensual / tamanio_tripulacion
   
  registrar_viaje

  actualizar_memoria
  
  ;; se reinicializa la memoria del viaje
  set distancia_recorrida 0
  set horas_en_mar 0
  set sitios_visitados []
  set capturas_viaje []

  ;; se consume tiempo y se cambian estados
  set tiempo_restante_iteracion tiempo_restante_iteracion - 1
  set tiempo_descansado 0
  set estado "descansando"
  if estado_economico = "quiebra" [
    move-to mi_puerto 
    set heading 90
    fd 1
    set activo? false
  ]

end

to-report obtener_balance_pesca
;  set ingreso_venta_captura (obtener_captura_total_viaje * PRECIO_BIOMASA)
  
  ;; se calcula el ingreso por venta de cada sp (como la captura está 
  ;; en toneladas y el precio en kg se multiplica por 1000
  let ingresos_venta_captura_por_sp (map [[c p] -> c * p * 1000] obtener_captura_por_sp_viaje precios_biomasa)
  set ingreso_venta_captura sum ingresos_venta_captura_por_sp
  set gasto_gasolina (PRECIO_LITRO_GAS * LITROS_POR_DISTANCIA * longitud_celda * distancia_recorrida) + (LITROS_POR_HORA_PESCA * length sitios_visitados)
  if saldo_subsidio_gasolina > 0 [
    ifelse saldo_subsidio_gasolina - gasto_gasolina > 0 [
      set saldo_subsidio_gasolina saldo_subsidio_gasolina - gasto_gasolina
      set gasto_gasolina 0
    ][
      set saldo_subsidio_gasolina 0
      set gasto_gasolina gasto_gasolina - saldo_subsidio_gasolina
    ]
  ]

  report ingreso_venta_captura - gasto_gasolina
end

to-report obtener_captura_por_sp_viaje
  let suma n-values _num_especies [ 0 ]
  foreach capturas_viaje [
    captura_sp ->
    set suma (map [[si ci] -> si + ci] suma captura_sp)
  ]
 report suma 
end
  
to registrar_viaje
  ;; se registra en las listas de viajes del dia
  set viajes_finalizados viajes_finalizados + 1 
  let _captura obtener_captura_total_viaje 
  let _ganancia ganancia
  let _horas_en_mar horas_en_mar
  let _ganancia_por_hora ganancia_por_hora
  let _gasto_gasolina gasto_gasolina
  let _distancia_recorrida distancia_recorrida
  ask mi_puerto [ 
    set capturas_dia_puerto lput _captura capturas_dia_puerto
    set capturas_mes_puerto lput _captura capturas_mes_puerto
    set ganancias_dia_puerto lput _ganancia ganancias_dia_puerto
    set ganancias_mes_puerto lput _ganancia ganancias_mes_puerto
    set horas_en_mar_dia_puerto lput _horas_en_mar horas_en_mar_dia_puerto
    set horas_en_mar_mes_puerto lput _horas_en_mar horas_en_mar_mes_puerto
    set ganancias_por_hora_dia_puerto lput _ganancia_por_hora ganancias_por_hora_dia_puerto
    set ganancias_por_hora_mes_puerto lput _ganancia_por_hora ganancias_por_hora_mes_puerto
    set gastos_gasolina_dia_puerto lput _gasto_gasolina gastos_gasolina_dia_puerto
    set gastos_gasolina_mes_puerto lput _gasto_gasolina gastos_gasolina_mes_puerto
    set distancias_recorridas_dia_puerto lput _distancia_recorrida distancias_recorridas_dia_puerto
    set distancias_recorridas_mes_puerto lput _distancia_recorrida distancias_recorridas_mes_puerto
    set viajes_finalizados_dia_puerto viajes_finalizados_dia_puerto + 1
    set viajes_finalizados_mes_puerto viajes_finalizados_mes_puerto + 1
  ]
  
  set captura_acumulada captura_acumulada + _captura
  set ganancia_acumulada ganancia_acumulada + _ganancia
  set gasto_gasolina_acumulada gasto_gasolina_acumulada + _gasto_gasolina
  set distancia_recorrida_acumulada distancia_recorrida_acumulada + _distancia_recorrida
  set horas_en_mar_acumulada horas_en_mar_acumulada + _horas_en_mar
  set n_acumulada n_acumulada + 1
   
end


to actualizar_memoria
  actualizar_memoria_EEI
  ;  actualizar_msemoria_EG
end

to actualizar_memoria_EEI
  let nuevo_mejor_sitio mejor_sitio
  (ifelse
    SELECCION_MEJOR_SITIO = "inicio de viaje" [
      set nuevo_mejor_sitio (item 0 sitios_visitados)
    ]
    SELECCION_MEJOR_SITIO = "mayor captura en viaje" [
      let sumas_captura_sitios_viaje []
      foreach capturas_viaje [
        captura_sitio -> 
        set sumas_captura_sitios_viaje lput (sum captura_sitio) sumas_captura_sitios_viaje
      ]
      set nuevo_mejor_sitio (item (obtener_indice_max sumas_captura_sitios_viaje) sitios_visitados)
    ]
  )

  if ganancia_por_hora > ganancia_por_hora_ultimo_viaje [
    set mejor_sitio nuevo_mejor_sitio
  ]
  set ganancia_por_hora_ultimo_viaje ganancia_por_hora
end

;; este procedimiento se actualiza mensualmente
to actualizar_estado_economico
  (ifelse 
    estado_economico = "viable" and salario_mensual_tripulacion < SALARIO_MENSUAL_MINIMO_ACEPTABLE [ 
      set estado_economico "crisis"
      set meses_crisis 1
    ]
    estado_economico = "crisis" and salario_mensual_tripulacion >= SALARIO_MENSUAL_MINIMO_ACEPTABLE [ 
      set meses_crisis 0  
      set estado_economico "viable" 
    ]
    estado_economico = "crisis" and salario_mensual_tripulacion <  SALARIO_MENSUAL_MINIMO_ACEPTABLE [ 
      ifelse meses_crisis < MESES_PARA_COLAPSO_EMBARCACION
      [ set meses_crisis meses_crisis + 1 ]
      [ set estado_economico "quiebra" ]
    ]
  )
end

;; este procdimiento se debe ejecutar después de actualizar la zonificación
;; y antes de recalcular una ruta
to actualizar_sitios_pesca_mejor_sitio_y_sitios_visitados
  set posibles_sitios_pesca celdas_libre with [num_region = [region_pesca] of myself ]
  let nuevo_mejor_sitio one-of posibles_sitios_pesca with-min [ distance [ mejor_sitio] of myself ]
  if nuevo_mejor_sitio = nobody [ show "Advertencia: No puedo encontrar un nuevo mejor sitio" ]
  foreach sitios_visitados [
    sitio -> 
    if member? sitio celdas_restriccion or member? sitio celdas_protegido [
      let pos position sitio sitios_visitados
      set sitios_visitados replace-item pos sitios_visitados nuevo_mejor_sitio
      show "Cambié mis sitios visitados"
    ]
  ]
  if member? mejor_sitio celdas_restriccion or member? mejor_sitio celdas_protegido [
    set mejor_sitio nuevo_mejor_sitio
    show "Cambié mi mejor sitio"
  ]
  if destino != nobody and (member? destino celdas_restriccion or member? destino celdas_protegido) [
    set destino nuevo_mejor_sitio
    show "Cambié mi destino"
  ]
  ;; si estoy planeando continuar un viaje y definen a todos mis vecinos como
  ;; protegidos entonces me quedo sin sitios candidatos para continuar mi pesca
  ;; por lo tanto me muevo primero a mi nuevo mejor sitio.
  if estado = "planeando" and tipo_planeacion = "continuar" [ 
    set estado "moviendose" 
  ]
  if estado = "moviendose" [
    set ruta obtener_ruta_A_star_embarcacion patch-here destino
    set indice_actual_ruta 0
  ]
  
  ;; cuando se generan celdas restricción para que no se rompa el modelo cuando buscan 
  ;; ruta se mueven las embarcaciones a un sitio cercano donde puedan estar
  ask celdas_restriccion [
    if any? embarcaciones-here [
      ask embarcaciones-here [
        move-to one-of posibles_sitios_pesca with-min [distance myself]
      ]
    ]
  ]
end